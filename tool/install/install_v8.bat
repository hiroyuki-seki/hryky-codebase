@echo //----------------------------------------------------------------
@echo // brief:
@echo //   build and install v8
@echo // usage:
@echo //   install_v8.bat
@echo // version:
@echo //   $Id: install_v8.bat 352 2014-06-08 08:48:59Z hryky.private@gmail.com $
@echo //----------------------------------------------------------------

@setlocal ENABLEDELAYEDEXPANSION ENABLEEXTENSIONS

@set TOOLROOT=%DEVROOT%/hryky-codebase/tool

@call %TOOLROOT%/build/define_variables.bat

@if "x64" == "%TARGET_ARCHITECTURE%" @(
	@set V8_ARCHITECTURE=x64
) else @(
	@set V8_ARCHITECTURE=ia32
)

@set MODULE=v8
@set MODULE_ROOT=%DEVROOT%\%MODULE%
@set MODULE_PROJDIR=%MODULE_ROOT%\build
@set INSTALL_ROOT=%DEVROOT%\hryky-codebase\external\v8
@set INSTALL_LIBDIR=%INSTALL_ROOT%\lib\win\%TARGET_ARCHITECTURE%\
@set INSTALL_INCDIR=%INSTALL_ROOT%\include\v8\
@set XCOPY=xcopy /F /Y 
@set PYTHON=%MODULE_ROOT%\third_party\python_26

@set DEVENVSLN=%MODULE_PROJDIR%\All.sln
@set DEVENVTARGET=
@set DEVENVCONFIG=Debug
@set DEVENVPLATFORM=%TARGET_ARCHITECTURE%

@set GYP_MSVS_VERSION=2013e

@cd %MODULE_ROOT%

@echo //--------------------------------
@echo // configure V8 : %TARGET_ARCHITECTURE%
@echo //--------------------------------

call third_party\python_26\python.exe build/gyp_v8 -Dtarget_arch=%V8_ARCHITECTURE%
@if errorlevel 1 @goto :error

@echo //--------------------------------
@echo // build V8
@echo // MSVC2012 can't build the project generated by GYP.
@echo //--------------------------------

call "%TOOLROOT%/build/open_solution.bat" %DEVENVSLN%
@if errorlevel 1 @goto :error

@echo //--------------------------------
@echo // install V8 : %TARGET_ARCHITECTURE%
@echo //--------------------------------

@if not exist "%INSTALL_LIBDIR%\Debug" @mkdir "%INSTALL_LIBDIR%\Debug"
%XCOPY% "%MODULE_ROOT%"\build\Debug\lib\*.lib "%INSTALL_LIBDIR%\Debug"

@if not exist "%INSTALL_LIBDIR%\Release" @mkdir "%INSTALL_LIBDIR%\Release"
%XCOPY% "%MODULE_ROOT%"\build\Release\lib\*.lib "%INSTALL_LIBDIR%\Release"

@if not exist "%INSTALL_INCDIR%" @mkdir "%INSTALL_INCDIR%"
%XCOPY% "%MODULE_ROOT%"\include "%INSTALL_INCDIR%\"


:success
@echo //----------------------------------------------------------------
@echo // %~f0 succeeded
@echo //----------------------------------------------------------------
@set RETCODE=0
@goto end

:error
@set RETCODE=1
@call %ASSERT% "%~f0 failed"
@goto end

:end

@exit /b %RETCODE%

@endlocal
